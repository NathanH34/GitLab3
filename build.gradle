plugins {
   id 'java'
   id "org.xbib.gradle.plugin.git" version "2.0.0"
   id "org.jbake.site" version "5.5.0" //jbake plugin
   id 'project-reports' //htmlDependencyReport
}

repositories {
    mavenCentral()
    //maven {
    //  url "https://plugins.gradle.org/m2/"
    //}
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.0' 
    testImplementation 'org.hamcrest:hamcrest-library:2.2'
    testImplementation("junit:junit:4.12")
    testRuntimeOnly("org.junit.vintage:junit-vintage-engine:5.5.2")
}

test {
    useJUnit()
    ignoreFailures = true
}

javadoc {
   options.with {
        //links 'https://docs.oracle.com/en/java/javase/11/docs/api/', 'gradle/javadocs/jdk'
   }
   mkdir 'build/tmp/website'
   mkdir 'build/tmp/website/assets'
   failOnError = false;
}

test.ignoreFailures=true
check.dependsOn htmlDependencyReport

task reports (dependsOn: ['javadoc', 'check']) {
    description 'Generate all reports for this project'
}

task clonePages() {
    doLast {
        mkdir 'build/gh-pages'
        def thisRepo = rootProject.projectDir.toString()
        def pagesDir = "$buildDir/gh-pages"
        project.delete {
            delete pagesDir
        }
        def grgit = git.clone {
            dir = pagesDir
            uri = thisRepo
            bare = false
            refToCheckout = 'gh-pages'
        }
        grgit.checkout {
            branch = 'gh-pages'
        }
    }
}

task copyDocs (type: Copy){
    from "$buildDir/docs"
    into "$buildDir/gh-pages/docs"
    dependsOn 'reports', 'clonePages'
}

task copyReports (type: Copy) {
    from "$buildDir/reports"
    into "$buildDir/gh-pages/reports"
    dependsOn 'reports', 'clonePages'
}

task updateGHPages (dependsOn: ['copyReports', 'copyDocs']) {
    group = "Reporting"
    description  'Copies reports to the gh-pages branch in preparation for a future push to GitHub'
    doLast {
        def pagesDir = "$buildDir/gh-pages"
        def grgit = git.open {
            dir = pagesDir
        }
        grgit.add (patterns: ['docs/'])
        grgit.add (patterns: ['reports/'])
        grgit.commit {
            message = "Updating web pages"
        }
        grgit.push {}
    }
}

task deployReports (dependsOn: 'updateGHPages') {
    description 'Deploy reports to website'
}


build.dependsOn javadoc